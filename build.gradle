plugins {
	id 'org.springframework.boot' version '3.1.4'
	id 'io.spring.dependency-management' version '1.1.3'
	id 'java'
	id 'com.github.node-gradle.node' version '7.0.1'
	id 'org.flywaydb.flyway' version '9.6.0'
	id 'io.freefair.aspectj.post-compile-weaving' version '8.4'
}

group = 'cz.forgottenempire'
version = '1.2.0'
sourceCompatibility = '17'

configurations {
	developmentOnly
	runtimeClasspath {
		extendsFrom developmentOnly
	}
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

node {
	// Set the work directory for unpacking node
	workDir = file("${project.buildDir}/nodejs")

	// Set the work directory for NPM
	npmWorkDir = file("${project.buildDir}/npm")
}

tasks.register('appNpmInstall', NpmTask) {
	description = "Installs all dependencies from package.json"
	workingDir = file("${project.projectDir}/src/main/webapp")
	args = ["install"]
}

tasks.register('appNpmBuild', NpmTask) {
	description = "Builds production version of the webapp"
	workingDir = file("${project.projectDir}/src/main/webapp")
	args = ["run", "build"]
}

tasks.register('copyWebApp', Copy) {
	from 'src/main/webapp/build'
	into 'build/resources/main/static/.'
}

// uncomment for prod build
appNpmBuild.dependsOn appNpmInstall
copyWebApp.dependsOn appNpmBuild
compileJava.dependsOn copyWebApp

dependencies {
	implementation 'org.aspectj:aspectjrt:1.9.20'
	implementation group: 'org.springframework', name: 'spring-aspects', version: '6.0.13'
	implementation 'org.springframework.boot:spring-boot-starter-aop'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-cache'
	implementation group: 'commons-io', name: 'commons-io', version: '2.7'
	implementation group: 'com.auth0', name: 'java-jwt', version: '4.0.0'
	implementation group: 'com.ibasco.agql', name: 'agql-source-query', version: '0.1.7'
	implementation group: 'org.freemarker', name: 'freemarker', version: '2.3.30'
	implementation group: 'org.springframework', name: 'spring-context-support', version: '5.2.6.RELEASE'
	implementation group: 'com.google.guava', name: 'guava', version: '30.0-android'
	implementation group: 'org.mapstruct', name: 'mapstruct', version: '1.5.2.Final'
	implementation group: 'org.flywaydb', name: 'flyway-core', version: '9.6.0'
	implementation group: 'org.jsoup', name: 'jsoup', version: '1.15.4'
	implementation 'org.flywaydb:flyway-mysql'

	compileOnly 'org.projectlombok:lombok'

	runtimeOnly 'com.mysql:mysql-connector-j'
	runtimeOnly 'com.h2database:h2'

	developmentOnly 'org.springframework.boot:spring-boot-devtools'

	aspect group: 'org.springframework', name: 'spring-aspects', version: '6.0.13'

	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
	annotationProcessor 'org.projectlombok:lombok'
	annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.3.Final'

	testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.23.1'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
}

test {
	useJUnitPlatform()
}
